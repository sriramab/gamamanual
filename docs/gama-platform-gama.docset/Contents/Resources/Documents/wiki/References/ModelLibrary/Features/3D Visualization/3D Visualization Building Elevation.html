<html><!-- Online page at https://github.com/gama-platform/gama/wiki/References/ModelLibrary/Features/3D Visualization/3D Visualization Building Elevation --><head><meta charset="utf-8"><title>3D Visualization Building Elevation</title><script src="../../../../../dash_resources/highlight.all.js"></script><link rel="stylesheet" type="text/css" href="../../../../../dash_resources/github-markdown.css"><link rel="stylesheet" type="text/css" href="../../../../../dash_resources/highlightjs.github.css"></head><body class="markdown-body" style="padding-bottom:20px; margin: 8px 13px;"><h1 id="3d-visualization-building-elevation">3D Visualization Building Elevation</h1>
<h1 id="3d-display-model-and-height-of-building-using-shapefiles">3D Display model and Height of Building using shapefiles</h1>
<p><em>Author : Arnaud Grignard</em></p>
<p>Model presenting a 3D display of people and buildings moving on a road network imported thanks to shapefiles. </p>
<p>Three experiments are proposed : one showing people represented by a yellow sphere moving from a living 3D building to a working 3D building and coming back using a road network (road_traffic). The second experiment distinguish the species by using different layers for species (road_traffic_multi_layer). The last one increases the Z location of the buildings and the people according to the time simulated (road_traffic_flying_off).</p>
<p>Code of the model : </p>
<pre><code class="hljs gams"><span class="hljs-keyword">model</span> tutorial_gis_city_traffic

global {
    <span class="hljs-comment">//Load of the different shapefiles used by the model</span>
    <span class="hljs-keyword">file</span> shape_file_buildings &lt;- shape_file(<span class="hljs-string">'../includes/building.shp'</span>, <span class="hljs-number">0</span>);
    <span class="hljs-keyword">file</span> shape_file_roads &lt;- shape_file(<span class="hljs-string">'../includes/road.shp'</span>, <span class="hljs-number">0</span>);
    <span class="hljs-keyword">file</span> shape_file_bounds &lt;- shape_file(<span class="hljs-string">'../includes/bounds.shp'</span>, <span class="hljs-number">0</span>);
    
    string texture &lt;- <span class="hljs-string">"../images/building_texture/texture1.jpg"</span>;
    string roof_texture &lt;- <span class="hljs-string">"../images/building_texture/roof_top.png"</span>;   
        
    <span class="hljs-comment">//Definition of the shape of the world as the bounds of the shapefiles to show everything contained</span>
    <span class="hljs-comment">// by the area delimited by the bounds</span>
    geometry shape &lt;- envelope(shape_file_bounds);
    int nb_people &lt;- <span class="hljs-number">100</span>;
    int day_time update: cycle <span class="hljs-built-in">mod</span> <span class="hljs-number">144</span>;
    int min_work_start &lt;- <span class="hljs-number">36</span>;
    int max_work_start &lt;- <span class="hljs-number">60</span>;
    int min_work_end &lt;- <span class="hljs-number">84</span>;
    int max_work_end &lt;- <span class="hljs-number">132</span>;
    float min_speed &lt;- <span class="hljs-number">50.0</span>;
    float max_speed &lt;- <span class="hljs-number">100.0</span>;
    
    <span class="hljs-comment">//Declaration of a graph that will represent our road network</span>
    graph the_graph;
    init {
        create building from: shape_file_buildings with: [type:: string(read(<span class="hljs-string">'NATURE'</span>))] {
            <span class="hljs-keyword">if</span> type = <span class="hljs-string">'Industrial'</span> {
                color &lt;- #blue;
            }
            height &lt;- <span class="hljs-number">10</span> + rnd(<span class="hljs-number">90</span>);
        }

        create road from: shape_file_roads;
        the_graph &lt;- as_edge_graph(road);
        list&lt;building&gt; residential_buildings &lt;- building where (each.type = <span class="hljs-string">'Residential'</span>);
        list&lt;building&gt; industrial_buildings &lt;- building where (each.type = <span class="hljs-string">'Industrial'</span>);
        create people number: nb_people {
            speed &lt;- min_speed + rnd(max_speed - min_speed);
            start_work &lt;- min_work_start + rnd(max_work_start - min_work_start);
            end_work &lt;- min_work_end + rnd(max_work_end - min_work_end);
            living_place &lt;- one_of(residential_buildings);
            working_place &lt;- one_of(industrial_buildings);
            location &lt;- any_location_in(living_place);
            location &lt;- {location.x, location.y,living_place.height };
        }

    }
}


species building {
    string type;
    rgb color &lt;- #gray;
    int height;
    aspect base {
        draw shape color: color depth: <span class="hljs-number">100</span>;<span class="hljs-comment">// texture:[roof_texture,texture];</span>
    }
}

species road {
    rgb color &lt;- #black;
    aspect base {
        draw shape color: color;
    }

}

species people skills: [moving] {
    rgb color &lt;- #yellow;
    building living_place &lt;- nil;
    building working_place &lt;- nil;
    int start_work;
    int end_work;
    string objectif;
    point the_target &lt;- nil;
    reflex time_to_work when: day_time = start_work {
        objectif &lt;- <span class="hljs-string">'working'</span>;
        the_target &lt;- any_location_in(working_place);
    }

    reflex time_to_go_home when: day_time = end_work {
        objectif &lt;- <span class="hljs-string">'go home'</span>;
        the_target &lt;- any_location_in(living_place);
    }

    reflex move when: the_target != nil {
        do goto target: the_target on: the_graph;
        switch the_target {
            match location {
                the_target &lt;- nil;
                location &lt;- {location.x, location.y,objectif  = <span class="hljs-string">'go home'</span> ?  living_place.height : working_place.height};
            }

        }

    }

    aspect base {
        draw sphere(<span class="hljs-number">10</span>) color: color;
    }

}



experiment road_traffic type: gui {
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Shapefile for the buildings:'</span> var: shape_file_buildings <span class="hljs-comment">category:</span> <span class="hljs-comment">'GIS'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Shapefile for the roads:'</span> var: shape_file_roads <span class="hljs-comment">category:</span> <span class="hljs-comment">'GIS'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Shapefile for the bounds:'</span> var: shape_file_bounds <span class="hljs-comment">category:</span> <span class="hljs-comment">'GIS'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Number of people agents'</span> var: nb_people <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Earliest hour to start work'</span> var: min_work_start <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Latest hour to start work'</span> var: max_work_start <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Earliest hour to end work'</span> var: min_work_end <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Latest hour to end work'</span> var: max_work_end <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'minimal speed'</span> var: min_speed <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'maximal speed'</span> var: max_speed <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    output {
        <span class="hljs-keyword">display</span> city_display type: opengl {
            species building aspect: base;
            species road aspect: base;
            species people aspect: base;
        }
        
        <span class="hljs-keyword">display</span> city_display2 {
            species building aspect: base;
            species road aspect: base;
            species people aspect: base;
        }
    }
}

experiment road_traffic_multi_layer type: gui {
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Shapefile for the buildings:'</span> var: shape_file_buildings <span class="hljs-comment">category:</span> <span class="hljs-comment">'GIS'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Shapefile for the roads:'</span> var: shape_file_roads <span class="hljs-comment">category:</span> <span class="hljs-comment">'GIS'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Shapefile for the bounds:'</span> var: shape_file_bounds <span class="hljs-comment">category:</span> <span class="hljs-comment">'GIS'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Number of people agents'</span> var: nb_people <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Earliest hour to start work'</span> var: min_work_start <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Latest hour to start work'</span> var: max_work_start <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Earliest hour to end work'</span> var: min_work_end <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'Latest hour to end work'</span> var: max_work_end <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'minimal speed'</span> var: min_speed <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    <span class="hljs-keyword">parameter</span> <span class="hljs-string">'maximal speed'</span> var: max_speed <span class="hljs-comment">category:</span> <span class="hljs-comment">'People'</span>;
    output {
        <span class="hljs-keyword">display</span> city_display type: opengl {
            species road aspect: base;
            species building aspect: base position:{<span class="hljs-number">0</span>,<span class="hljs-number">0</span>,<span class="hljs-number">0.25</span>};
            species people aspect: base position:{<span class="hljs-number">0</span>,<span class="hljs-number">0</span>,<span class="hljs-number">0.5</span>};
        }
    }

}

experiment road_traffic_flying_off type: gui {
    output {
        <span class="hljs-keyword">display</span> flyingOffAgents type: opengl {
            species road aspect: base;
            species building aspect: base position: { <span class="hljs-number">0</span>, <span class="hljs-number">0</span>, (time * <span class="hljs-number">2</span>)/<span class="hljs-number">1000</span> };
            species people aspect: base position: { <span class="hljs-number">0</span>, <span class="hljs-number">0</span>, (time * <span class="hljs-number">4</span>)/<span class="hljs-number">1000</span> };
        }
    }
}
</code></pre>
<script>hljs.initHighlightingOnLoad();</script></body></html>