<html><!-- Online page at https://github.com/gama-platform/gama/wiki/References/ModelLibrary/Features/Driving Skill/Driving Skill Road Traffic simple (City) --><head><meta charset="utf-8"><title>Driving Skill Road Traffic simple (City)</title><script src="../../../../../dash_resources/highlight.all.js"></script><link rel="stylesheet" type="text/css" href="../../../../../dash_resources/github-markdown.css"><link rel="stylesheet" type="text/css" href="../../../../../dash_resources/highlightjs.github.css"></head><body class="markdown-body" style="padding-bottom:20px; margin: 8px 13px;"><h1 id="driving-skill-road-traffic-simple-city">Driving Skill Road Traffic simple (City)</h1>
<h1 id="simple-road-network">Simple Road Network</h1>
<p><em>Author : Patrick Taillandier</em></p>
<p>Model using shapefiles to create buildings and a road graph, with people going from their living place to their work place depending on the hour. The traffic jam is also taken into account to slow the people agents when they are too much on the same road. The experiment shows a display of the city, with people agents, buildings and roads, a display of the traffic jam occuring on the roads, and a chart display showing two charts : one for the traffic jam coefficients, and an other for the objectives of the people agents.</p>
<p>Code of the model : </p>
<pre><code class="hljs processing">  
model RoadTrafficCity
 
   
global {   
    
    <span class="hljs-comment">//Shapefiles for the buildings, the roads and the bounds of the environment</span>
    file shape_file_roads parameter: <span class="hljs-string">"Shapefile for the roads:"</span> category: <span class="hljs-string">"GIS"</span> &lt;- file(<span class="hljs-string">"../includes/ManhattanRoads.shp"</span>) ;
    file shape_file_bounds parameter: <span class="hljs-string">"Shapefile for the bounds:"</span> category: <span class="hljs-string">"GIS"</span> &lt;- file(<span class="hljs-string">"../includes/ManhattanBounds.shp"</span>) ;
    file shape_file_buildings parameter: <span class="hljs-string">"Shapefile for the buildings:"</span> category: <span class="hljs-string">"GIS"</span> &lt;- file(<span class="hljs-string">"../includes/ManhattanBuildings.shp"</span>) ;
    geometry <span class="hljs-built_in">shape</span> &lt;- envelope(shape_file_bounds);
    
    <span class="hljs-comment">//Stock the number of times agents reached their goal (their house or work place)</span>
    <span class="hljs-built_in">int</span> nbGoalsAchived &lt;- <span class="hljs-number">0</span>;
    
    <span class="hljs-comment">//represent the day time for the agent to inform them to go work or home</span>
    <span class="hljs-built_in">int</span> day_time update: cycle mod <span class="hljs-number">144</span> ;
    
    <span class="hljs-comment">//Variables to manage the minimal and maximal time to start working</span>
    <span class="hljs-built_in">int</span> min_work_start &lt;- <span class="hljs-number">36</span>;
    <span class="hljs-built_in">int</span> max_work_start &lt;- <span class="hljs-number">60</span>;
    
    <span class="hljs-comment">//Number of people created</span>
    <span class="hljs-built_in">int</span> nb_people &lt;- <span class="hljs-number">500</span>;
    
    <span class="hljs-comment">//Variables to manage the minimal and maximal time to go home</span>
    <span class="hljs-built_in">int</span> min_work_end &lt;- <span class="hljs-number">84</span>; 
    <span class="hljs-built_in">int</span> max_work_end &lt;- <span class="hljs-number">132</span>; 
    
    <span class="hljs-comment">//Manage the speed allowed in the model for the people agents</span>
    <span class="hljs-built_in">float</span> min_speed &lt;- <span class="hljs-number">50.0</span>;
    <span class="hljs-built_in">float</span> max_speed &lt;- <span class="hljs-number">100.0</span>; 
    
    <span class="hljs-comment">//Graph of the road network</span>
    graph the_graph;
    
     
    init {  
        
        <span class="hljs-comment">//creation of the agents of road and building species using the shapefile and linking the </span>
        create road from: shape_file_roads with:[nbLanes::<span class="hljs-built_in">int</span>(read(<span class="hljs-string">"lanes"</span>))];
        create building from: shape_file_buildings;
        
        <span class="hljs-comment">//Increase the shape of roads according to the number of lanes it has</span>
        ask road as list {
            visu_geom &lt;- <span class="hljs-built_in">shape</span> + (<span class="hljs-number">2</span> * nbLanes); 
        }
        <span class="hljs-comment">//Initliazation of the graph with the road species</span>
        the_graph &lt;-  (as_edge_graph(road));
        
        <span class="hljs-comment">//Initialization of nb_people agents of people species</span>
        <span class="hljs-comment">// and definition of their living and working places</span>
        create people number: nb_people { 
            living_space &lt;- <span class="hljs-number">3.0</span>;
            tolerance &lt;- <span class="hljs-number">0.1</span>;
            lanes_attribute &lt;- <span class="hljs-string">"nbLanes"</span>;
            obstacle_species &lt;- [species(self)]; 
            speed &lt;- min_speed + rnd (max_speed - min_speed) ;
            start_work &lt;- min_work_start + rnd (max_work_start - min_work_start) ;
            end_work &lt;- min_work_end + rnd (max_work_end - min_work_end) ;
            living_place &lt;- one_of(building) ;
            working_place &lt;- one_of(building) ;
            location &lt;- living_place.location; 
        }   
    }
    
    <span class="hljs-comment">//Update of the graph every 10 Cycles to take into account the traffic jam of the road in the weights of the graph</span>
    reflex update_graph when:every(<span class="hljs-number">10</span>){
        <span class="hljs-built_in">map</span>&lt;road,<span class="hljs-built_in">float</span>&gt; weights_map &lt;- road as_map (each:: (each.<span class="hljs-built_in">shape</span>.perimeter * each.coeff_traffic));
        the_graph &lt;- the_graph with_weights weights_map;
    }
    
} 
    
species road  { 
    <span class="hljs-built_in">int</span> nbLanes;
    <span class="hljs-built_in">int</span> indexDirection; 
    bool blocked &lt;- <span class="hljs-keyword">false</span>;
    rgb <span class="hljs-built_in">color</span> &lt;- #black;
    <span class="hljs-built_in">float</span> coeff_traffic &lt;- <span class="hljs-number">1.0</span> update: <span class="hljs-number">1</span> + (<span class="hljs-built_in">float</span>(length(people at_distance <span class="hljs-number">1.0</span>)) / <span class="hljs-built_in">shape</span>.perimeter * <span class="hljs-number">200</span> / nbLanes);
    geometry visu_geom;
    
    aspect base { 
        <span class="hljs-title">draw</span> <span class="hljs-built_in">shape</span> <span class="hljs-built_in">color</span>: #black ;
    } 
    
    
    <span class="hljs-comment">//Command that the user can execute to remove or add a road </span>
    user_command <span class="hljs-string">"Remove a road"</span> action: remove;
    user_command <span class="hljs-string">"Add a road"</span> action: <span class="hljs-built_in">add</span>;
         
    action remove {
        blocked &lt;- <span class="hljs-keyword">true</span>;
        the_graph &lt;-  (as_edge_graph(road where (!each.blocked))) ;
        <span class="hljs-built_in">map</span>&lt;road,<span class="hljs-built_in">float</span>&gt; weights_map &lt;- road as_map (each:: each.coeff_traffic);
        the_graph &lt;- the_graph  with_weights weights_map;
        <span class="hljs-built_in">color</span> &lt;- #magenta;
    }
        
    action <span class="hljs-built_in">add</span> {
        blocked &lt;- <span class="hljs-keyword">false</span>;
        the_graph &lt;-  (as_edge_graph(road where (!each.blocked)));
        <span class="hljs-built_in">map</span>&lt;road,<span class="hljs-built_in">float</span>&gt; weights_map &lt;- road as_map (each:: each.coeff_traffic);
        the_graph &lt;- the_graph  with_weights weights_map;
        <span class="hljs-built_in">color</span> &lt;- #black;
    }
        
    aspect road_width {  
        <span class="hljs-title">draw</span> visu_geom <span class="hljs-built_in">color</span>: <span class="hljs-built_in">color</span> ;
    }
    
    aspect traffic_jam {  
        <span class="hljs-keyword">if</span> (coeff_traffic &gt; <span class="hljs-number">0.025</span>) {
            <span class="hljs-title">draw</span> <span class="hljs-built_in">shape</span> + (coeff_traffic / <span class="hljs-number">4.0</span>) <span class="hljs-built_in">color</span>: #<span class="hljs-built_in">red</span> ;
        }
    }       
}
    
species building  { 
    rgb <span class="hljs-built_in">color</span> &lt;- #gray;
    aspect base { 
        <span class="hljs-title">draw</span> <span class="hljs-built_in">shape</span> <span class="hljs-built_in">color</span>: <span class="hljs-built_in">color</span> ;
    }
}
    
species people skills: [driving]{ 
    <span class="hljs-built_in">float</span> speed; 
    rgb <span class="hljs-built_in">color</span> &lt;- rgb([rnd(<span class="hljs-number">255</span>),rnd(<span class="hljs-number">255</span>),rnd(<span class="hljs-number">255</span>)]) ;
    <span class="hljs-built_in">point</span> targetBis &lt;- nil ; 
    <span class="hljs-built_in">point</span> previousLoc &lt;- nil;
    bool normalMove &lt;- <span class="hljs-keyword">true</span>;
    <span class="hljs-built_in">float</span> evadeDist &lt;- <span class="hljs-number">500.0</span>;
    building living_place &lt;- nil ;
    building working_place &lt;- nil ;
    <span class="hljs-built_in">int</span> start_work ;
    <span class="hljs-built_in">int</span> end_work  ;
    string objective ; 
    <span class="hljs-built_in">point</span> the_target &lt;- nil ;
        
    <span class="hljs-comment">//Reflex to make the agent move while it had a target and normalMove equals true</span>
    reflex move when: the_target != nil and normalMove{
        previousLoc &lt;- <span class="hljs-built_in">copy</span>(location);
        do goto_driving target: the_target on: the_graph speed: speed ; 
        <span class="hljs-keyword">switch</span> location { 
            <span class="hljs-built_in">match</span> the_target {
                the_target &lt;- nil;
                nbGoalsAchived &lt;- nbGoalsAchived +<span class="hljs-number">1</span>;
            }
            <span class="hljs-built_in">match</span> previousLoc {
                targetBis &lt;- last((one_of(road where (each distance_to self &lt; evadeDist)).<span class="hljs-built_in">shape</span>).points);
                normalMove &lt;- <span class="hljs-keyword">false</span>;
            }
        }
    }
        
    <span class="hljs-comment">//Reflex to make the agent move when it is not normal moving </span>
    reflex EvadeMove when: !(normalMove){
        previousLoc &lt;- <span class="hljs-built_in">copy</span>(location);
        do goto_driving target: targetBis on: the_graph speed: speed ; 
        <span class="hljs-keyword">switch</span> location { 
            <span class="hljs-built_in">match</span> targetBis {
                normalMove &lt;- <span class="hljs-keyword">true</span>;
            }
            <span class="hljs-built_in">match</span> previousLoc {
                targetBis &lt;- last((one_of(road where (each distance_to self &lt; evadeDist)).<span class="hljs-built_in">shape</span>).points);
            }
        }
    }
    
    <span class="hljs-comment">//Reflex to make the agent go to its working place when it's time to go work</span>
    reflex time_to_work when: day_time = start_work {
        objective &lt;- <span class="hljs-string">"working"</span> ;
        the_target &lt;- any_location_in (working_place);
    }
    
    <span class="hljs-comment">//Reflex to make the agent go to its living place when it's time to go home</span>
    reflex time_to_go_home when: day_time = end_work {
        objective &lt;- <span class="hljs-string">"go home"</span> ;
        the_target &lt;- any_location_in (living_place); 
    }  
    
    aspect base {
        <span class="hljs-title">draw</span> circle(<span class="hljs-number">20</span>) <span class="hljs-built_in">color</span>: <span class="hljs-built_in">color</span>;
    }
}

experiment traffic type: gui {
    parameter <span class="hljs-string">"Shapefile for the buildings:"</span> var: shape_file_buildings category: <span class="hljs-string">"GIS"</span> ;
    parameter <span class="hljs-string">"Shapefile for the roads:"</span> var: shape_file_roads category: <span class="hljs-string">"GIS"</span> ;
    parameter <span class="hljs-string">"Shapefile for the bounds:"</span> var: shape_file_bounds category: <span class="hljs-string">"GIS"</span> ;
    parameter <span class="hljs-string">"Number of people agents"</span> var: nb_people category: <span class="hljs-string">"People"</span> ;
    parameter <span class="hljs-string">"Earliest hour to start work"</span> var: min_work_start category: <span class="hljs-string">"People"</span> ;
    parameter <span class="hljs-string">"Latest hour to start work"</span> var: max_work_start category: <span class="hljs-string">"People"</span> ;
    parameter <span class="hljs-string">"Earliest hour to end work"</span> var: min_work_end category: <span class="hljs-string">"People"</span> ;
    parameter <span class="hljs-string">"Latest hour to end work"</span> var: max_work_end category: <span class="hljs-string">"People"</span> ;
    parameter <span class="hljs-string">"minimal speed"</span> var: min_speed category: <span class="hljs-string">"People"</span> ;
    parameter <span class="hljs-string">"maximal speed"</span> var: max_speed category: <span class="hljs-string">"People"</span> ;
    
    output {
        display city_display {
            species road aspect: road_width ;
            species building aspect: base;
            species people aspect: base;
        }
        display traffic_jam_display {
            species road aspect: base ;
            species road aspect: traffic_jam ;
        }
        display chart_display refresh: every(<span class="hljs-number">10</span>) {
            chart <span class="hljs-string">"Traffic jam"</span> type: series <span class="hljs-built_in">size</span>: {<span class="hljs-number">0.9</span>, <span class="hljs-number">0.4</span>} position: {<span class="hljs-number">0.05</span>, <span class="hljs-number">0.05</span>} {
                data <span class="hljs-string">"Mean road traffic coefficient"</span> value: mean (road collect each.coeff_traffic) style: <span class="hljs-built_in">line</span> <span class="hljs-built_in">color</span>: #<span class="hljs-built_in">green</span> ;
                data <span class="hljs-string">"Max road traffic coefficient"</span> value: road max_of (each.coeff_traffic) style: <span class="hljs-built_in">line</span> <span class="hljs-built_in">color</span>: #<span class="hljs-built_in">red</span> ;
            }
            chart <span class="hljs-string">"People Objectif"</span> type: pie style: exploded <span class="hljs-built_in">size</span>: {<span class="hljs-number">0.9</span>, <span class="hljs-number">0.4</span>} position: {<span class="hljs-number">0.05</span>, <span class="hljs-number">0.55</span>} {
                data <span class="hljs-string">"Working"</span> value: length ((people as list) where (each.objective=<span class="hljs-string">"working"</span>)) <span class="hljs-built_in">color</span>: #<span class="hljs-built_in">green</span> ;
                data <span class="hljs-string">"Staying home"</span> value: length ((people as list) where (each.objective=<span class="hljs-string">"go home"</span>)) <span class="hljs-built_in">color</span>: #<span class="hljs-built_in">blue</span> ;
            }
        }
        monitor <span class="hljs-string">"Number of goals achieved"</span> value: nbGoalsAchived ;
    }
}


</code></pre>
<script>hljs.initHighlightingOnLoad();</script></body></html>