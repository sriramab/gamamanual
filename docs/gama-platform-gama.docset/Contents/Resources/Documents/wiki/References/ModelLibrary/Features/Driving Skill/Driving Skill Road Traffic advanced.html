<html><!-- Online page at https://github.com/gama-platform/gama/wiki/References/ModelLibrary/Features/Driving Skill/Driving Skill Road Traffic advanced --><head><meta charset="utf-8"><title>Driving Skill Road Traffic advanced</title><script src="../../../../../dash_resources/highlight.all.js"></script><link rel="stylesheet" type="text/css" href="../../../../../dash_resources/github-markdown.css"><link rel="stylesheet" type="text/css" href="../../../../../dash_resources/highlightjs.github.css"></head><body class="markdown-body" style="padding-bottom:20px; margin: 8px 13px;"><h1 id="driving-skill-road-traffic-advanced">Driving Skill Road Traffic advanced</h1>
<h1 id="complex-road-network">Complex Road Network</h1>
<p><em>Author : Patrick Taillandier</em></p>
<p>Model to show how to use the driving skill to represent the traffic on a road network generated thanks to shapefiles, with intersections and traffic lights going from red to green to let people move or stop. Two experiments are presented : experiment_2D to display the model in 2D and which better display the orientation of roads and experiment_3D to display the model in 3D.</p>
<p>Code of the model : </p>
<pre><code class="hljs processing">
model RoadTrafficComplex
 
global {   
    bool simple_data &lt;- <span class="hljs-keyword">false</span>;
    
    <span class="hljs-comment">//Check if we use simple data or more complex roads</span>
    file shape_file_roads  &lt;- simple_data ? file(<span class="hljs-string">"../includes/RoadCircleLanes.shp"</span>): file(<span class="hljs-string">"../includes/ManhattanRoads.shp"</span>) ;
    file shape_file_nodes  &lt;- simple_data ? file(<span class="hljs-string">"../includes/NodeCircleLanes.shp"</span>) : file(<span class="hljs-string">"../includes/ManhattanNodes.shp"</span>);
    file shape_file_bounds &lt;- simple_data ? file(<span class="hljs-string">"../includes/BoundsLaneRoad.shp"</span>) :file(<span class="hljs-string">"../includes/ManhattanRoads.shp"</span>);
    geometry <span class="hljs-built_in">shape</span> &lt;- envelope(shape_file_bounds) + <span class="hljs-number">50.0</span>;
    
    graph road_network;  
    <span class="hljs-built_in">int</span> nb_people &lt;- simple_data ? <span class="hljs-number">20</span> : <span class="hljs-number">500</span>;
     
    init {  
        <span class="hljs-comment">//create the intersection and check if there are traffic lights or not by looking the values inside the type column of the shapefile and linking</span>
        <span class="hljs-comment">// this column to the attribute is_traffic_signal. </span>
        create intersection from: shape_file_nodes with:[is_traffic_signal::(read(<span class="hljs-string">"type"</span>) = <span class="hljs-string">"traffic_signals"</span>)];
        
        <span class="hljs-comment">//create road agents using the shapefile and using the oneway column to check the orientation of the roads if there are directed</span>
        create road from: shape_file_roads with:[lanes::<span class="hljs-built_in">int</span>(read(<span class="hljs-string">"lanes"</span>)), oneway::string(read(<span class="hljs-string">"oneway"</span>))] {
            geom_display &lt;- <span class="hljs-built_in">shape</span> + (<span class="hljs-number">2.5</span> * lanes);
            maxspeed &lt;- (lanes = <span class="hljs-number">1</span> ? <span class="hljs-number">30.0</span> : (lanes = <span class="hljs-number">2</span> ? <span class="hljs-number">50.0</span> : <span class="hljs-number">70.0</span>)) °km/°h;
            <span class="hljs-keyword">switch</span> oneway {
                <span class="hljs-built_in">match</span> <span class="hljs-string">"no"</span> {
                    create road {
                        lanes &lt;- <span class="hljs-built_in">max</span>([<span class="hljs-number">1</span>, <span class="hljs-built_in">int</span> (myself.lanes / <span class="hljs-number">2.0</span>)]);
                        <span class="hljs-built_in">shape</span> &lt;- polyline(<span class="hljs-built_in">reverse</span>(myself.<span class="hljs-built_in">shape</span>.points));
                        maxspeed &lt;- myself.maxspeed;
                        geom_display  &lt;- myself.geom_display;
                        linked_road &lt;- myself;
                        myself.linked_road &lt;- self;
                    }
                    lanes &lt;- <span class="hljs-built_in">int</span>(lanes /<span class="hljs-number">2.0</span> + <span class="hljs-number">0.5</span>);
                }
                <span class="hljs-built_in">match</span> <span class="hljs-string">"-1"</span> {
                    <span class="hljs-built_in">shape</span> &lt;- polyline(<span class="hljs-built_in">reverse</span>(<span class="hljs-built_in">shape</span>.points));
                }   
            }
        }   
        <span class="hljs-built_in">map</span> general_speed_map &lt;- road as_map (each::(each.<span class="hljs-built_in">shape</span>.perimeter / each.maxspeed));
        
        <span class="hljs-comment">//creation of the road network using the road and intersection agents</span>
        road_network &lt;-  (as_driving_graph(road, intersection))  with_weights general_speed_map;
        
        <span class="hljs-comment">//initialize the traffic light</span>
        ask intersection {
            do initialize;
        }
        
        create people number: nb_people { 
            max_speed &lt;- <span class="hljs-number">160</span> °km/°h;
            vehicle_length &lt;- <span class="hljs-number">5.0</span> °m;
            right_side_driving &lt;- <span class="hljs-keyword">true</span>;
            proba_lane_change_up &lt;- <span class="hljs-number">0.1</span> + (rnd(<span class="hljs-number">500</span>) / <span class="hljs-number">500</span>);
            proba_lane_change_down &lt;- <span class="hljs-number">0.5</span>+ (rnd(<span class="hljs-number">500</span>) / <span class="hljs-number">500</span>);
            location &lt;- one_of(intersection where empty(each.stop)).location;
            security_distance_coeff &lt;- <span class="hljs-number">5</span>/<span class="hljs-number">9</span> * <span class="hljs-number">3.6</span> * (<span class="hljs-number">1.5</span> - rnd(<span class="hljs-number">1000</span>) / <span class="hljs-number">1000</span>);  
            proba_respect_priorities &lt;- <span class="hljs-number">1.0</span> - rnd(<span class="hljs-number">200</span>/<span class="hljs-number">1000</span>);
            proba_respect_stops &lt;- [<span class="hljs-number">1.0</span>];
            proba_block_node &lt;- <span class="hljs-number">0.0</span>;
            proba_use_linked_road &lt;- <span class="hljs-number">0.0</span>;
            max_acceleration &lt;- <span class="hljs-number">5</span>/<span class="hljs-number">3.6</span>;
            speed_coeff &lt;- <span class="hljs-number">1.2</span> - (rnd(<span class="hljs-number">400</span>) / <span class="hljs-number">1000</span>);
            threshold_stucked &lt;-<span class="hljs-built_in">int</span> ( (<span class="hljs-number">1</span> + rnd(<span class="hljs-number">5</span>))°mn);
            proba_breakdown &lt;- <span class="hljs-number">0.00001</span>;
            
        }   
    }
    
} 

<span class="hljs-comment">//species that will represent the intersection node, it can be traffic lights or not, using the skill_road_node skill</span>
species intersection skills: [skill_road_node] {
    bool is_traffic_signal;
    list&lt;list&gt; stop;
    <span class="hljs-built_in">int</span> time_to_change &lt;- <span class="hljs-number">100</span>;
    <span class="hljs-built_in">int</span> counter &lt;- rnd (time_to_change) ;
    list&lt;road&gt; ways1;
    list&lt;road&gt; ways2;
    bool is_green;
    rgb color_fire;
    
    action initialize {
        <span class="hljs-keyword">if</span> (is_traffic_signal) {
            do compute_crossing;
            stop&lt;&lt; [];
            <span class="hljs-keyword">if</span> (flip(<span class="hljs-number">0.5</span>)) {
                do to_green;
            } <span class="hljs-keyword">else</span> {
                do to_red;
            }   
        }
    }
    
    action compute_crossing{
        <span class="hljs-keyword">if</span>  (length(roads_in) &gt;= <span class="hljs-number">2</span>) {
            road rd0 &lt;- road(roads_in[<span class="hljs-number">0</span>]);
            list&lt;<span class="hljs-built_in">point</span>&gt; pts &lt;- rd0.<span class="hljs-built_in">shape</span>.points;                        
            <span class="hljs-built_in">float</span> ref_angle &lt;-  <span class="hljs-built_in">float</span>( last(pts) direction_to rd0.location);
            <span class="hljs-built_in">loop</span> rd over: roads_in {
                list&lt;<span class="hljs-built_in">point</span>&gt; pts2 &lt;- road(rd).<span class="hljs-built_in">shape</span>.points;                      
                <span class="hljs-built_in">float</span> angle_dest &lt;-  <span class="hljs-built_in">float</span>( last(pts2) direction_to rd.location);
                <span class="hljs-built_in">float</span> ang &lt;- <span class="hljs-built_in">abs</span>(angle_dest - ref_angle);
                <span class="hljs-keyword">if</span> (ang &gt; <span class="hljs-number">45</span> and ang &lt; <span class="hljs-number">135</span>) or  (ang &gt; <span class="hljs-number">225</span> and ang &lt; <span class="hljs-number">315</span>) {
                    ways2&lt;&lt; road(rd);
                }
            }
        }
        <span class="hljs-built_in">loop</span> rd over: roads_in {
            <span class="hljs-keyword">if</span> not(rd in ways2) {
                ways1 &lt;&lt; road(rd);
            }
        }
    }
    
    action to_green {
        stop[<span class="hljs-number">0</span>] &lt;- ways2 ;
        color_fire &lt;- #<span class="hljs-built_in">green</span>;
        is_green &lt;- <span class="hljs-keyword">true</span>;
    }
    
    action to_red {
        stop[<span class="hljs-number">0</span>] &lt;- ways1;
        color_fire &lt;- #<span class="hljs-built_in">red</span>;
        is_green &lt;- <span class="hljs-keyword">false</span>;
    }
    reflex dynamic_node when: is_traffic_signal  {
        counter &lt;- counter + <span class="hljs-number">1</span>;
        <span class="hljs-keyword">if</span> (counter &gt;= time_to_change) { 
            counter &lt;- <span class="hljs-number">0</span>;
            <span class="hljs-keyword">if</span> is_green {do to_red;}
            <span class="hljs-keyword">else</span> {do to_green;}
        } 
    }
    
    aspect base {
        <span class="hljs-keyword">if</span> (is_traffic_signal) {    
            <span class="hljs-title">draw</span> circle(<span class="hljs-number">5</span>) <span class="hljs-built_in">color</span>: color_fire;
        }
    }
    
    aspect base3D {
        <span class="hljs-keyword">if</span> (is_traffic_signal) {    
            <span class="hljs-title">draw</span> <span class="hljs-built_in">box</span>(<span class="hljs-number">1</span>,<span class="hljs-number">1</span>,<span class="hljs-number">10</span>) <span class="hljs-built_in">color</span>:#black;
            <span class="hljs-title">draw</span> <span class="hljs-built_in">sphere</span>(<span class="hljs-number">5</span>) at: {location.x,location.y,<span class="hljs-number">12</span>} <span class="hljs-built_in">color</span>: color_fire;
        }
    }
}


<span class="hljs-comment">//species that will represent the roads, it can be directed or not and uses the skill skill_road</span>
species road skills: [skill_road] { 
    geometry geom_display;
    string oneway;
    aspect base {    
        <span class="hljs-title">draw</span> <span class="hljs-built_in">shape</span> <span class="hljs-built_in">color</span>: #gray end_arrow: <span class="hljs-number">10</span>;
    } 
    aspect base3D {    
        <span class="hljs-title">draw</span> geom_display <span class="hljs-built_in">color</span>: #gray ;
    } 
}

<span class="hljs-comment">//People species that will move on the graph of roads to a target and using the skill advanced_driving</span>
species people skills: [advanced_driving] { 
    rgb <span class="hljs-built_in">color</span> &lt;- rgb(rnd(<span class="hljs-number">255</span>), rnd(<span class="hljs-number">255</span>), rnd(<span class="hljs-number">255</span>)) ;
    <span class="hljs-built_in">int</span> counter_stucked &lt;- <span class="hljs-number">0</span>;
    <span class="hljs-built_in">int</span> threshold_stucked;
    bool breakdown &lt;- <span class="hljs-keyword">false</span>;
    <span class="hljs-built_in">float</span> proba_breakdown ;
    intersection target;
    
    reflex breakdown when: flip(proba_breakdown){
        breakdown &lt;- <span class="hljs-keyword">true</span>;
        max_speed &lt;- <span class="hljs-number">1</span> °km/°h;
    }
    
    reflex time_to_go when: final_target = nil {
        target &lt;- one_of(intersection where not each.is_traffic_signal);
        current_path &lt;- compute_path(graph: road_network, target: target );
        <span class="hljs-keyword">if</span> (current_path = nil ) {
            final_target &lt;- nil;
        }
    }
    reflex move when: current_path != nil and final_target != nil {
        do drive;
        <span class="hljs-keyword">if</span> real_speed &lt; <span class="hljs-number">5</span>°km/°h {
            counter_stucked&lt;- counter_stucked + <span class="hljs-number">1</span>;
            <span class="hljs-keyword">if</span> (counter_stucked mod threshold_stucked = <span class="hljs-number">0</span>) {
                proba_use_linked_road &lt;- <span class="hljs-built_in">min</span>([<span class="hljs-number">1.0</span>,proba_use_linked_road + <span class="hljs-number">0.1</span>]);
            }
        } <span class="hljs-keyword">else</span> {
            counter_stucked&lt;- <span class="hljs-number">0</span>;
            proba_use_linked_road &lt;- <span class="hljs-number">0.0</span>;
        }
    }

    aspect base { 
        <span class="hljs-title">draw</span> breakdown ? square(<span class="hljs-number">15</span>) : <span class="hljs-built_in">triangle</span>(<span class="hljs-number">15</span>) <span class="hljs-built_in">color</span>: <span class="hljs-built_in">color</span> <span class="hljs-built_in">rotate</span>:heading + <span class="hljs-number">90</span>;
    } 
    aspect base3D {
        <span class="hljs-built_in">point</span> loc &lt;- calcul_loc();
        <span class="hljs-title">draw</span> <span class="hljs-built_in">box</span>(vehicle_length, <span class="hljs-number">1</span>,<span class="hljs-number">1</span>) at: loc <span class="hljs-built_in">rotate</span>:  heading <span class="hljs-built_in">color</span>: <span class="hljs-built_in">color</span>;
        
        <span class="hljs-title">draw</span> <span class="hljs-built_in">triangle</span>(<span class="hljs-number">0.5</span>) depth: <span class="hljs-number">1.5</span> at: loc <span class="hljs-built_in">rotate</span>:  heading + <span class="hljs-number">90</span> <span class="hljs-built_in">color</span>: <span class="hljs-built_in">color</span>;
        
        <span class="hljs-keyword">if</span> (breakdown) {
            <span class="hljs-title">draw</span> circle(<span class="hljs-number">2</span>) at: loc <span class="hljs-built_in">color</span>: #<span class="hljs-built_in">red</span>;
        }
    } 
    
    <span class="hljs-built_in">point</span> calcul_loc {
        <span class="hljs-keyword">if</span> (current_road = nil) {
            <span class="hljs-keyword">return</span> location;
        } <span class="hljs-keyword">else</span> {
            <span class="hljs-built_in">float</span> val &lt;- (road(current_road).lanes - current_lane) + <span class="hljs-number">0.5</span>;
            val &lt;- on_linked_road ? val * - <span class="hljs-number">1</span> : val;
            <span class="hljs-keyword">if</span> (val = <span class="hljs-number">0</span>) {
                <span class="hljs-keyword">return</span> location; 
            } <span class="hljs-keyword">else</span> {
                <span class="hljs-keyword">return</span> (location + {<span class="hljs-built_in">cos</span>(heading + <span class="hljs-number">90</span>) * val, <span class="hljs-built_in">sin</span>(heading + <span class="hljs-number">90</span>) * val});
            }
        }
    }
    
} 

experiment experiment_2D type: gui {
    parameter <span class="hljs-string">"if true, simple data (simple track), if false complex one (Manhattan):"</span> var: simple_data category: <span class="hljs-string">"GIS"</span> ;
    output {
        display city_display {
            species road aspect: base ;
            species intersection aspect: base;
            species people aspect: base;
        }
    }
}

experiment experiment_3D type: gui {
    parameter <span class="hljs-string">"if true, simple data (simple track), if false complex one (Manhattan):"</span> var: simple_data category: <span class="hljs-string">"GIS"</span> ;
    output {
        display carte_principale type: opengl {
            species road aspect: base3D refresh: <span class="hljs-keyword">true</span>;
            species intersection aspect: base3D;
            species people aspect: base3D ; 
        }
    }
}
</code></pre>
<script>hljs.initHighlightingOnLoad();</script></body></html>